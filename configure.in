dnl
dnl configure.in
dnl	autoconf input file for mgv.
dnl
dnl Copyright (C) 1998 Eric A. Howe
dnl
dnl This program is free software; you can redistribute it and/or modify
dnl it under the terms of the GNU General Public License as published by
dnl the Free Software Foundation; either version 2 of the License, or
dnl (at your option) any later version.
dnl
dnl This program is distributed in the hope that it will be useful,
dnl but WITHOUT ANY WARRANTY; without even the implied warranty of
dnl MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
dnl GNU General Public License for more details.
dnl
dnl You should have received a copy of the GNU General Public License
dnl along with this program; if not, write to the Free Software
dnl Foundation, Inc., 675 Mass Ave, Cambridge, MA 02139, USA.
dnl
dnl   Authors:	Eric A. Howe (mu@trends.net)
dnl
dnl Much of the X checking was borrowed from DDD.
dnl

dnl
dnl Process this file with autoconf to produce a configure script.
dnl
AC_INIT(mgv.c)
AC_CONFIG_AUX_DIR(tools)
AM_INIT_AUTOMAKE(mgv, 4.0.0-alpha4)

dnl
dnl This is a trick to get my CVS ident string into configure.
dnl AC_REVISION could be used but that strips out the dollar signs and
dnl thus prevents `ident' from figuring out the revision information.
dnl
AC_OUTPUT_COMMANDS([# @(#)$Mu: mgv/configure.in 1.56 2001/03/14 15:01:19 $])

dnl
dnl See config/cc.m4 if you want to know why I'm not using AC_PROG_CC.
dnl autoconf-2.13/automake-1.4 will produce warnings about CC not being
dnl defined if AC_PROG_CC is not found in configure.in; hence, we fool
dnl auto* with the argument to MU_PROG_CC (note that MU_PROG_CC doesn't
dnl really take any arguments).  This looks pretty sick because it is
dnl pretty sick.
dnl
MU_PROG_CC([AC_PROG_CC])
AC_PROG_RANLIB

dnl ==========================================================================
dnl Makefile things.
dnl ==========================================================================

AC_ARG_ENABLE(maintainer,
[  --enable-maintainer     enable maintainer rules [default=no]])
AM_CONDITIONAL(MU_MAINTAINER, test x$enable_maintainer = xyes)

AC_MSG_CHECKING(if we need _POSIX_SOURCE for sigaction)
AC_TRY_COMPILE([#include <signal.h>], [struct sigaction x;],
	use_posix_source=yes,
	use_posix_source=no)
AC_MSG_RESULT($use_posix_source)
if test "$use_posix_source" = yes; then
	##
	## We have to change CFLAGS here because we want _POSIX_SOURCE
	## to be used for other tests. The putenv thing is a major hack
	## but I can't see any other way to get it work.
	##
	CFLAGS="$CFLAGS -D_POSIX_SOURCE"
	AC_DEFINE(HAVE_PUTENV, 0)
else
	AC_DEFINE(HAVE_PUTENV, 1)
fi

dnl
dnl Checks for libraries.
dnl
AC_PATH_XTRA
MU_FIND_MOTIF
MU_FIND_XPM
LIBS="$LIBS $X_EXTRA_LIBS"
CFLAGS="$CFLAGS $X_CFLAGS"
CPPFLAGS="$CPPFLAGS $X_CFLAGS"
LDFLAGS="$LDFLAGS $X_LIBS"

dnl
dnl Check for X11 and friends.
dnl
X11_LIBS=""
AC_CHECK_LIB(X11, XOpenDisplay, X11_LIBS="-lX11",,${X_PRE_LIBS})
if test "$X11_LIBS" = ""; then
AC_MSG_ERROR([The X11 library '-lX11' could not be found.
		Please use the configure options '--x-includes=DIR'
		and '--x-libraries=DIR' to specify the X location.
		See the 'config.log' file for further diagnostics.])
fi
AC_SUBST(X_LIBS)
AC_SUBST(X11_LIBS)
AC_SUBST(X_PRE_LIBS)

XEXT_LIBS=""
AC_CHECK_LIB(Xext, XShapeQueryVersion, XEXT_LIBS="-lXext",,
	${X_PRE_LIBS} ${X11_LIBS})
AC_SUBST(XEXT_LIBS)

##
## Some versions of Motif (RedHat's Motif 2.1 for instance) require
## you to link with -lXp.  mgv doesn't use Xp but it can't hurt to link
## it in anyway.
##
XP_LIBS=""
AC_CHECK_LIB(Xp, XpGetContext, XP_LIBS="-lXp",,
	${X_PRE_LIBS} ${XEXT_LIBS} ${X11_LIBS})
AC_SUBST(XP_LIBS)

XT_LIBS=""
AC_CHECK_LIB(Xt, XtToolkitInitialize, XT_LIBS="-lXt",,
	${X_PRE_LIBS} ${X11_LIBS})
if test "$XT_LIBS" = ""; then
AC_MSG_ERROR([The X toolkit library '-lXt' could not be found.
		Please use the configure iptions '--x-includes=DIR'
		and '--x-libraries=DIR' to specify the X location.
		See the 'config.log' file for further diagnostics.])
fi
AC_SUBST(XT_LIBS)

XPM_LIBS=""
if test "x${xpm_libraries}" != "xno"; then
	AC_CHECK_LIB(Xpm, XpmCreatePixmapFromXpmImage,
		XPM_LIBS="-lXpm"
		AC_DEFINE(HAVE_XPM, 1), AC_DEFINE(HAVE_XPM, 0),
		${X_PRE_LIBS} ${X11_LIBS})
	if test "$XPM_LIBS" != ""; then
		AC_CHECK_HEADERS(X11/xpm.h)
	fi
else
	AC_DEFINE(HAVE_XPM, 0)
fi
AC_SUBST(XPM_LIBS)

XM_LIBS=""
AC_CHECK_LIB(Xm, XmRepTypeInstallTearOffModelConverter,
	XM_LIBS="-lXm"
	AC_DEFINE(HAVE_MOTIF, 1), AC_DEFINE(HAVE_MOTIF, 0),
	${X_PRE_LIBS} ${XPM_LIBS} ${XEXT_LIBS} ${XT_LIBS} ${XP_LIBS} ${XEXT_LIBS} ${X11_LIBS})
if test "$XM_LIBS" = ""; then
AC_MSG_ERROR([The Motif library '-lXm' could not be found.
		Please use the configure options '--with-motif-includes=DIR'
		and '--with-motif-libraries=DIR' to specify the Xm location.
		See the 'config.log' file for further diagnostics.])
fi
AC_SUBST(XM_LIBS)

EDITRES_LIBS=""
AC_ARG_WITH(editres,
[  --without-editres                do not use editres])
if test "x${with_editres}" != "xno"; then
	AC_CHECK_LIB(Xmu, _XEditResCheckMessages,
		EDITRES_LIBS="-lXmu"
		AC_DEFINE(HAVE_EDITRES, 1), AC_DEFINE(HAVE_EDITRES, 0),
		${X_PRE_LIBS} ${XEXT_LIBS} ${XT_LIBS} ${XEXT_LIBS} ${X11_LIBS})
else
	AC_DEFINE(HAVE_EDITRES, 0)
fi
AC_SUBST(EDITRES_LIBS)

AC_OUTPUT([Makefile doc/Makefile wlib/Makefile lib/Makefile lib/toolbar/Makefile])

dnl ==========================================================================
dnl Print some information about what we found.
dnl ==========================================================================
echo
echo "Configuration information for $PACKAGE $VERSION"
echo "C compiler          = $CC"
echo "C compiler flags    = $CFLAGS"
echo "defs                = $DEFS"
echo "link flags          = $LDFLAGS"
echo "X11/Motif libraries = $XM_LIBS $EDITRES_LIBS $XT_LIBS $XPM_LIBS $XP_LIBS $XEXT_LIBS $X11_LIBS"
if test "x${EDITRES_LIBS}" != "x"; then
	echo "editres support     = yes ($EDITRES_LIBS)"
else
	echo "editres support     = no"
fi
if test "x${XPM_LIBS}" != "x"; then
	echo "xpm support         = yes ($XPM_LIBS)"
else
	echo "xpm support         = no"
fi
if test "x${enable_compiledhelp}" = "xyes"; then
	echo "compiled-in help    = yes"
else
	echo "compiled-in help    = no"
fi
